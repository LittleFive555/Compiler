// TODO 现在for的分析表中有多条定义，需要消除

S: MultiStatement | ·



// 语句
MultiStatement: Statement MultiStatement | Statement

SemicolonOrNot: Semicolon

Statement:    AssignStatement    |     CodeBlock     |     Function Statement2      |     IfStatement     |     ForStatement     |     ForeachStatement | WhileStatement | ClassStatement | ReturnStatement | BreakStatement | ContinueStatement | SwitchStatement | CaseStatement | DefaultStatement | TryStatement | ThrowStatement | AwaitStatement | AsyncStatement


Statement2:Identifier FunctionParams CodeBlock | FunctionParams CodeBlock

// 运算
Value: Operation1

Operation1: Operation1 And Operation2 | Operation1 Minus Operation2 | Operation2 

Operation2: Operation2 InclusiveOr Operation3 | Operation2 Combine Operation3 | Operation2 XOR Operation3 | Operation2 LShift Operation3 | Operation2 RShift Operation3 | Operation3

Operation3: Operation3 Equal Operation4 | Operation3 NotEqual Operation4 | Operation3 EqualReference Operation4 | Operation3 NotEqualReference Operation4 | Operation3 Greater Operation4 | Operation3 GreaterOrEqual Operation4 | Operation3 Less Operation4 | Operation3 LessOrEqual Operation4 | Operation4

Operation4: Operation4 Plus Operation5 | Operation4 Minus Operation5 | Operation5

Operation5: Operation5 Multiply Factor | Operation5 Divide Factor | Operation5 Modulo Factor | Factor 

Factor: Object | LeftPar Value RightPar



// 声明和赋值
AssignStatement: Var CanAssignValue CanAssignValueList AssignValue | CanAssignValue AssignStatement1

AssignStatement1:Comma CanAssignValue CanAssignValueList AssignValue | AssignSymbol Value | LeftPar FunctionParamsRight AssignStatement11 | CallMember | IndexMember | ·

AssignStatement11:CodeBlock | ·

CanAssignValue: Identifier | Identifier CallMember | Identifier IndexMember | MultiAssign

MultiAssign: LeftBracket Identifier MultiAssign1

MultiAssign1: RightBracket | Comma IdentifierList RightBracket

IdentifierList: Identifier IdentifierList1 | ·

IdentifierList1: Comma IdentifierList | Identifier | ·

ValueList: Value | ValueList Comma Value

AssignValue: AssignSymbol Value | ·

CanAssignValueList: Comma CanAssignValue CanAssignValueList | ·

AssignSymbol: Assign | PlusAssign | MinusAssign | MultiplyAssign | DivideAssign | ModuloAssign | CombineAssign | InclusiveOrAssign | XORAssign | ShrAssign | ShiAssign



Object: Number | Identifier | Boolean | Identifier FunctionOrMember

FunctionOrMember: FunctionParams | CallMember | IndexMember

IsFuncCallOrNot: FunctionParams | ·

IndexMember: LeftBracket Value RightBracket IndexMember | LeftBracket Value RightBracket

CallMember: Period Identifier IsFuncCallOrNot CallMember | Period Identifier IsFuncCallOrNot



// 函数
FunctionDefine : Function Identifier FunctionParams CodeBlock | Identifier FunctionParams CodeBlock | Function FunctionParams CodeBlock

FunctionParams : LeftPar FunctionParamsRight

FunctionParamsRight: ValueList RightPar | RightPar



// 代码块
CodeBlock : LeftBrace MultiStatement RightBrace | LeftBrace RightBrace



// 布尔值
Boolean : True | False



// if语句
IfStatement : If LeftPar Value RightPar CodeBlock NotIfPart

NotIfPart : ElseIf LeftPar Value RightPar CodeBlock NotIfPart | ElsePart

ElsePart: Else CodeBlock | ·



// for语句 (存在间接左递归，会无法判断两种for，可能需要去除间接左递归，或者引入回溯)
ForStatement: For LeftPar ForPart

// 此处限定for第一部分只能出现赋值语句，但实际上的Scorpio实现不止可以赋值，而可以任何语句
ForPart: AssignStatement ForNormalPartTwo | ForNormalPartTwo | Var Identifier AssignValue Comma Value RightPar CodeBlock | Identifier AssignValue Comma Value RightPar CodeBlock

// for语句 for(;;)
ForNormalPartTwo: Semicolon Value ForNormalPartThree | Semicolon ForNormalPartThree

ForNormalPartThree: Semicolon MultiStatement RightPar CodeBlock | Semicolon RightPar CodeBlock



// foreach语句
ForeachStatement: Foreach LeftPar Var Identifier In Object RightPar CodeBlock



// while语句
WhileStatement: While LeftPar Value RightPar CodeBlock



// class定义
ClassStatement: Class Identifier ClassInherit

ClassInherit: Colon Identifier ClassContent | ClassContent

ClassContent: LeftBrace ClassMultiFunctionDefine RightBrace | LeftBrace RightBrace

ClassMultiFunctionDefine: ClassMultiFunctionDefine ClassFunctionDefine | ClassFunctionDefine

ClassFunctionDefine: AsyncStatement | Get Identifier FunctionParams CodeBlock | FunctionDefine



// return
ReturnStatement: Return | Return Value



// break
BreakStatement: Break



// continue
ContinueStatement: Continue



// switch
SwitchStatement: Switch LeftPar Value RightPar CodeBlock

CaseStatement: Case Value Colon

DefaultStatement: Default Colon



// 异常
TryStatement: Try CodeBlock Catch LeftPar Identifier RightPar CodeBlock

ThrowStatement: Throw Value



// 异步
AwaitStatement: Await Value
AsyncStatement: Async FunctionDefine